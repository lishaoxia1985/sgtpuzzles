import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

static def timestamp() {
    def dateFormat = new SimpleDateFormat("yyyy-MM-dd")
    dateFormat.setTimeZone(TimeZone.getTimeZone("UTC"))
    return dateFormat.format(new Date())
}

def idForAndroidFrontEnd() {
    try {
        def shortCommitId = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = shortCommitId
        }
        return shortCommitId.toString().trim()
    } catch (ignored) {
        return "UNOFFICIAL"
    }
}

def idForPuzzles() {
    try {
        def status = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'submodule', 'status'
            standardOutput = status
        }
        return status.toString().trim().substring(0, 8)
    } catch (ignored) {
        return "UNOFFICIAL"
    }
}

android {
    compileSdkVersion 29
    defaultConfig {
        targetSdkVersion 28
        minSdkVersion 16
        applicationId "name.boyle.chris.sgtpuzzles"
        versionCode 120
        versionName "${timestamp()}-${idForAndroidFrontEnd()}-${idForPuzzles()}"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true

        fileTree(dir: 'src/main/jni/puzzles', include: '*.c').each { File f ->
            def game = f.name.replace(".c", "")
            def text = f.text
            def match = text =~ /enum\s+\{\s*COL_[^,]+,\s*(COL_[^}]+)}/
            if (match) {
                def colours = match.group(1).replaceAll(/(?s)\/\*.*?\*\//, "").replaceAll(/#[^\n]*\n/, "")
                        .trim().split(",")*.trim()*.replaceFirst(/^COL_/, "")*.toLowerCase()
                        .findAll { it =~ /^[^=]+$/ } - ["ncolours", "crossedline"]
                if (colours.any { it =~ /[^a-z0-9_]/ }) {
                    throw new GradleException("Couldn't parse colours for " + game + ": " + match.group(1) + " -> " + colours)
                }
                resValue "string", game + "_colours", colours.join(',')
                //println "\t<string translatable=\"false\" name=\"" + game + "_colours\">" + colours.join(',') + "</string>"
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }
    buildTypes {
        debug {
            ndk {
                debuggable true
            }
        }
        release {
            minifyEnabled true
            ndk {
                debuggable false
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '29.0.2'
    ndkVersion '21.3.6528147'
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation 'androidx.annotation:annotation:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
